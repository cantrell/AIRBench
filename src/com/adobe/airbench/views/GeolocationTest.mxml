<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:components="com.adobe.airbench.components.*"
		title="Geolocation"
		viewActivate="onViewActivate();"
		viewDeactivate="onViewDeactivate();">
	
	<fx:Script>
		<![CDATA[
			import com.adobe.airbench.model.ModelLocator;
			
			import flash.sensors.Geolocation;
			
			private const GEOLOCATION:String = "geolocation";
			private var ml:ModelLocator;
			private var geo:Geolocation;
			private var updateCalled:Boolean;
			
			private function onViewActivate():void
			{
				this.ml = ModelLocator.getInstance();
				this.updateCalled = false;
				if (!Geolocation.isSupported)
				{
					this.ml.testManager.reportCapabilitiesTestResults(GEOLOCATION, false, "Geolocation not supported.");
					this.navBar.testComplete(false, "Geolocation unsupported");
					return;
				}
				this.testGeolocation();
			}
			
			private function onViewDeactivate():void
			{
				if (this.geo != null)
				{
					this.geo.removeEventListener(GeolocationEvent.UPDATE, onUpdate);
					this.geo.removeEventListener(StatusEvent.STATUS, onStatus);
					this.geo = null;
				}
			}

			private function testGeolocation():void
			{
				this.geo = new Geolocation();
				this.geo.addEventListener(GeolocationEvent.UPDATE, onUpdate);
				this.geo.addEventListener(StatusEvent.STATUS, onStatus);
			}
			
			private function onUpdate(e:GeolocationEvent):void
			{
				this.latitude.text = String(e.latitude);
				this.longitude.text = String(e.longitude);
				this.altitude.text = String(e.altitude);
				this.heading.text = String(e.heading);
				this.speed.text = String(e.speed);
				this.horizontalAccuracy.text = String(e.horizontalAccuracy);
				this.verticalAccuracy.text = String(e.verticalAccuracy);
				this.timestamp.text = String(e.timestamp);
				if (!this.updateCalled)
				{
					this.updateCalled = true;
					this.ml.testManager.reportCapabilitiesTestResults(GEOLOCATION, true, "Geolocation data available.");
					this.navBar.testComplete(true, "Geolocation works!");
				}
			}
			
			private function onStatus(e:StatusEvent):void
			{
				if (!this.geo) return;
				if (this.geo.muted)
				{
					this.ml.testManager.reportCapabilitiesTestResults(GEOLOCATION, false, "Geolocation is muted.");
					this.navBar.testComplete(false, "Geolocation muted");
				}
			}
			
		]]>
	</fx:Script>
	
	<s:VGroup width="100%" height="100%">
		<s:Scroller width="100%" height="100%">
			<s:Group width="100%" height="100%">

				<s:Label text="Latitude:"            right="250" y="100"/>	
				<s:Label text="Longitude:"           right="250" y="150"/>	
				<s:Label text="Altitude:"            right="250" y="200"/>	
				<s:Label text="Heading:"             right="250" y="250"/>	
				<s:Label text="Speed:"               right="250" y="300"/>	
				<s:Label text="Horizontal Accuracy:" right="250" y="350"/>	
				<s:Label text="Vertical Accuracy:"   right="250" y="400"/>	
				<s:Label text="Timestamp:"           right="250" y="450"/>	
				
				<s:Label id="latitude"            left="250" y="100" text="0"/>	
				<s:Label id="longitude"           left="250" y="150" text="0"/>	
				<s:Label id="altitude"            left="250" y="200" text="0"/>	
				<s:Label id="heading"             left="250" y="250" text="0"/>	
				<s:Label id="speed"               left="250" y="300" text="0"/>	
				<s:Label id="horizontalAccuracy"  left="250" y="350" text="0"/>	
				<s:Label id="verticalAccuracy"    left="250" y="400" text="0"/>	
				<s:Label id="timestamp"           left="250" y="450" text="0"/>	
				
			</s:Group>
		</s:Scroller>
		<components:NavigationBar id="navBar" width="100%"/>
	</s:VGroup>
</s:View>
