<?xml version="1.0" encoding="utf-8"?>
<s:View xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark"
		xmlns:components="com.adobe.airbench.components.*"
		title="XML Perf Test"
		viewActivate="onViewActivate();"
		viewDeactivate="onViewDeactivate();">

		<fx:Script>
			<![CDATA[
				import com.adobe.airbench.model.ModelLocator;
				
				private const XML_PARSING:String = "xml_parsing";
				private var ml:ModelLocator;
				
				private function onViewActivate():void
				{
					this.ml = ModelLocator.getInstance();
					var xmlFile:File = File.applicationDirectory.resolvePath("test_assets/subscriptions.xml");
					var fs:FileStream = new FileStream();
					fs.addEventListener(Event.COMPLETE, onXmlFileLoaded);
					fs.openAsync(xmlFile, FileMode.READ);
				}
				
				private function onXmlFileLoaded(e:Event):void
				{
					var fs:FileStream = e.target as FileStream;
					fs.removeEventListener(Event.COMPLETE, onXmlFileLoaded);
					var fileBytes:ByteArray = new ByteArray();
					fs.readBytes(fileBytes);
					fs.close();
					var kb:String = String(Math.round(fileBytes.length / 1000)) + " KB";
					this.description.text = this.description.text.replace("$1", kb);
					var startTime:Number = new Date().time;
					var testXml:XML = new XML(fileBytes);
					var parsedXml:String = testXml.toXMLString();
					var totalTime:Number = new Date().time - startTime;
					this.output.text = "XML Parse time: " + String(totalTime) + " ms";
					this.ml.testManager.reportPerformanceTestResults(XML_PARSING, totalTime, "XML document parsed in " + String(totalTime) + " ms.");
					this.navBar.testComplete(true, "Test Complete");
					System.disposeXML(testXml);
				}
				
				private function onViewDeactivate():void
				{
					
				}
				
			]]>
		</fx:Script>
	<s:VGroup width="100%" height="100%">
		<s:Scroller width="100%" height="100%">
			<s:VGroup width="100%" height="100%" verticalAlign="middle" horizontalAlign="center" gap="20">
				<components:MultiLineLabel id="description" width="100%" text="This test measures how long it takes to parse a $1 XML file."/>
				<s:Label id="output" horizontalCenter="0" fontWeight="bold"/>
			</s:VGroup>
		</s:Scroller>
		<components:NavigationBar id="navBar" width="100%"/>
	</s:VGroup>
</s:View>
